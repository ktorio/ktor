apply from: rootProject.file('gradle/jdk.gradle')

def jdk = 6

if (project.name in project.ext.jdk8Modules) {
    jdk = 8
} else if (project.name in project.ext.jdk7Modules) {
    jdk = 7
}

apply plugin: "kotlinx-atomicfu"

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
    }

    task jarTest(type: Jar, dependsOn: jvmTestClasses) {
        from targets.jvm.compilations.test.output
        classifier 'test'
    }

    configure(targets) {
        compilations.all { compilation ->
            if (compilation.name == "main") {
                def compileTask = tasks.getByName(compilation.compileKotlinTaskName)
                if (!compileTask.name.endsWith("Jvm")) return

                // dump declarations from main JVM module for binary-compatibility-validator
                compileTask.kotlinOptions.freeCompilerArgs += ["-Xdump-declarations-to=${buildDir}/visibilities.json"]

                switch (jdk) {
                    case 8:
                        compileTask.kotlinOptions.jdkHome = System.env.JDK_18
                        break
                    case 7:
                        compileTask.kotlinOptions.jdkHome = System.env.JDK_17 ?: System.env.JDK_18
                        break
                    default:
                        compileTask.kotlinOptions.jdkHome = System.env.JDK_16 ?: System.env.JDK_17 ?: System.env.JDK_18
                        break
                }
            }
        }
    }

    sourceSets {
        jvmMain.dependencies {
            api group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
            api group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version
            if (jdk > 6) {
                api group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk7', version: kotlin_version
            }
            if (jdk > 7) {
                api group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
            }
            api("org.jetbrains.kotlinx:atomicfu:$atomic_fu_version")

            api group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
            api group: 'com.typesafe', name: 'config', version: '1.3.1'
        }

        jvmTest.dependencies {
            api 'org.jetbrains.kotlin:kotlin-test'
            api 'org.jetbrains.kotlin:kotlin-test-junit'
            api "junit:junit:$junit_version"

            api group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit', version: kotlin_version
            api group: 'junit', name: 'junit', version: junit_version

            api "org.jetbrains.kotlinx:atomicfu:$atomic_fu_version"
            implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-debug:1.1.1'
        }
    }
}

dependencies {
    if (jdk > 7) {
        jvmMainApi(group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-jdk8', version: coroutines_version) {
            exclude(module: 'kotlin-stdlib')
            exclude(module: 'kotlin-stdlib-jvm')
            exclude(module: 'kotlin-stdlib-jdk8')
            exclude(module: 'kotlin-stdlib-jdk7')
        }
    }

    jvmMainApi(group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: coroutines_version)  {
        exclude(module: 'kotlin-stdlib')
        exclude(module: 'kotlin-stdlib-jvm')
    }

    jvmMainApi(group: 'org.jetbrains.kotlinx', name: 'kotlinx-io-jvm', version: kotlinx_io_version) {
        exclude(module: 'kotlin-stdlib-jvm')
    }

    jvmMainApi(group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-io-jvm', version: kotlinx_io_version) {
        exclude(module: 'kotlin-stdlib-jvm')
        exclude(module: 'kotlinx-coroutines')
        exclude(module: 'kotlinx-coroutines-jdk8')
    }
}

jvmTest {
    ignoreFailures = true
    maxHeapSize = "2g"
    exclude("**/*StressTest*")
}

task stressTest(type: Test) {
    classpath = files { jvmTest.classpath }
    testClassesDirs = files { jvmTest.testClassesDirs }

    ignoreFailures = true
    maxHeapSize = "2g"
    forkEvery = 1
    include("**/*StressTest*")
    systemProperty "enable.stress.tests", "true"
}

configurations {
    testOutput.extendsFrom(testCompile)
    boot
}

jvmJar {
    manifest {
        attributes("Implementation-Title": project.name, "Implementation-Version": configuredVersion)
    }
}
