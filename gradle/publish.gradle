apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

apply from: rootProject.file('gradle/platform.gradle')

def platform = platformOf(project)
def skipDokka = project.path.contains("ktor-samples") || project == rootProject || project.name in dokkaExcludes || platform != 'jvm'

if (!skipDokka) {
    apply from: rootProject.file('gradle/dokka.gradle')
}

publishing {
    repositories {
        maven {
            if (publishLocal) {
                url globalM2
            } else {
                url 'https://dl.bintray.com/kotlin/ktor'
            }
        }
    }

    publications {
        mavenProject(MavenPublication) {
            if (platform != 'native') {
                from components.java
            }
            groupId project.group
            artifactId project.name
            version project.version

            artifact sourceJar

            if (!skipDokka) {
                artifact javadocJar {
                    classifier 'javadoc'
                }

                artifact dokkaJar {
                    classifier 'kdoc'
                }
            }
        }
    }
}

def skipUpload = project.path.contains("ktor-samples")
if (!skipUpload) {
    apply from: rootProject.file("gradle/bintray.gradle")
}
