apply plugin: 'maven'
apply plugin: 'maven-publish'

apply from: project.rootProject.file('gradle/pom.gradle')

task emptyJar(type: Jar) {
}

def selectArtifactId(project, type, defaultName) {
    def name = project.name
    def isCommon = project.ext.has("commonStructure")
    def commonIsRoot = project.ext.has("commonStructure") && project.ext.commonStructure
    def hasNative = project.ext.has("hasNative") && project.ext.hasNative

    switch (type) {
        case 'metadata':
            if (!isCommon) return "$name-$type"
            return "$name-metadata"
            break
        case 'kotlinMultiplatform':
            if (!hasNative) return "$name-kotlinMultiplatform"
            return name
            break
        case 'jvm':
            return commonIsRoot ? "$name-jvm" : "$name"
            break
        default:
            return isCommon || hasNative ? defaultName : "$name"
            break
    }
}

def isAvailableForPublication(publication) {
    def name = publication.name
    if (name in ['maven']) return true

    def result = false
    result = result | (name in ['jvm', 'androidRelease', 'androidDebug', 'js', 'jsLegacy', 'metadata', 'kotlinMultiplatform'])
    result = result | (project.ext.isLinuxHost && name == 'linuxX64')
    result = result | (project.ext.isWinHost && name == 'mingwX64')
    result = result | (project.ext.isMacosHost && name in ['iosX64', 'iosArm32', 'iosArm64', 'macosX64', 'watchosArm32', 'watchosArm64', 'watchosX86', 'tvosArm64', 'tvosX64'])

    return result
}

tasks.withType(AbstractPublishToMaven).all {
    onlyIf { isAvailableForPublication(publication) }
}

publishing {
    repositories {
        maven {
            if (publishLocal) {
                url globalM2
            } else {
                def user = 'kotlin'
                def repo = 'ktor'
                def name = 'ktor'
                url = "https://api.bintray.com/maven/$user/$repo/$name/;publish=0;override=1"
                credentials {
                    username = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
                    password = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
                }
            }
        }
        maven { name = "testLocal"; url = "$rootProject.buildDir/m2" }
    }
    publications.all {
        pom.withXml(configureMavenCentralMetadata)

        def type = it.name
        def id = selectArtifactId(project, type, it.artifactId)
        it.artifactId = id

        if (name == "kotlinMultiplatform") {
            it.artifact(emptyJar) { classifier 'javadoc' }
            it.artifact(emptyJar) { classifier 'kdoc' }
            it.artifact emptyJar
        }
    }

    if (rootProject.ext.nonDefaultProjectStructure.contains(project.name)) return

    kotlin.targets.all { target ->
        def publication = publishing.publications.findByName(target.name)

        if (publication != null) {
            if (target.platformType.name == 'jvm') {
                publication.artifact(emptyJar) {
                    classifier 'javadoc'
                }
                publication.artifact dokkaJar
            } else {
                publication.artifact(emptyJar) {
                    classifier 'javadoc'
                }
                publication.artifact(emptyJar) {
                    classifier 'kdoc'
                }
            }

            if (target.platformType.name == 'native') {
                publication.artifact emptyJar
            }
        }
    }
}

publish.dependsOn publishToMavenLocal

class PublishWithRetryTask extends DefaultTask {
    private def retryCount = Integer.getInteger("io.ktor.publish.retry", 100)

    String description = "Retry task until success"
    Collection<Task> publishTasks

    @TaskAction
    void exec() {
        def attempt = 1
        while (attempt <= retryCount) {
            try {
                publishTasks.forEach { Task task -> task.publish() }
                break
            } catch (Throwable cause) {
                attempt += 1
                println("Failed to execute publish task from $attempt attempt. Retry")
                cause.printStackTrace()
            }
        }
    }
}

task publishWithRetry(type: PublishWithRetryTask) {
    description "Publish maven publications with retry"
    dependsOn publishToMavenLocal
    publishTasks = tasks.withType(PublishToMavenRepository.class)
}
