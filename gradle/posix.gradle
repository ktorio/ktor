apply from: rootProject.file("gradle/ide.gradle")

apply plugin: "kotlinx-atomicfu"

if (project.name != 'ktor-client-curl') {
    apply from: rootProject.file("gradle/native.gradle")
}

def getHostName() {
    def target = System.getProperty("os.name")
    if (target == 'Linux') return 'linux'
    if (target.startsWith('Windows')) return 'windows'
    if (target.startsWith('Mac')) return 'macos'
    return 'unknown'
}

/**
 * Disable cross compilation for missing cinterop.
 * @param targets
 */
def disableCompilation(targets) {
    configure(targets) {
        compilations.all {
            cinterops.all { project.tasks[interopProcessingTaskName].enabled = false }
            compileKotlinTask.enabled = false
        }
        binaries.all { linkTask.enabled = false }

        mavenPublication { publicationToDisable ->
            tasks.withType(AbstractPublishToMaven).all {
                onlyIf { publication != publicationToDisable }
            }
            tasks.withType(GenerateModuleMetadata).all {
                onlyIf { publication.get() != publicationToDisable }
            }
        }
    }
}

kotlin {
    targets {
        if (project.ext.ideaActive) {
            fromPreset(project.ext.ideaPreset, 'posix')
            project.ext.nativeTargets = [posix]
        } else {
            fromPreset(presets.linuxX64, 'linuxX64')
            fromPreset(presets.macosX64, 'macosX64')
            fromPreset(presets.mingwX64, 'mingwX64')
            project.ext.nativeTargets = [macosX64, linuxX64, mingwX64]

            if (project.name != 'ktor-client-curl') {
                fromPreset(presets.iosArm64, 'iosArm64')
                fromPreset(presets.iosArm32, 'iosArm32')
                fromPreset(presets.iosX64, 'iosX64')

                project.ext.nativeTargets = [iosX64, iosArm64, iosArm32, macosX64, linuxX64, mingwX64]
            }


            def crossCompileTargets = [linuxX64, macosX64, mingwX64]
            def host = getHostName()
            switch (host) {
                case 'linux':
                    crossCompileTargets.remove(linuxX64)
                    break
                case 'windows':
                    crossCompileTargets.remove(mingwX64)
                    break
                case 'macos':
                    crossCompileTargets.remove(macosX64)
                    break
            }
            disableCompilation(crossCompileTargets)
            configure([mingwX64Test]) {
                environment "PATH": "C:\\msys64\\mingw64\\bin;C:\\Tools\\msys64\\mingw64\\bin"
            }
        }

        project.ext.nativeCompilations = project.ext.nativeTargets.collect { it.compilations.main }
    }
    sourceSets {
        posixMain.dependencies {
            implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core-native', version: coroutines_version
            implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-io-native', version: kotlinx_io_version
            implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-io-native', version: kotlinx_io_version

            implementation "org.jetbrains.kotlinx:atomicfu:$atomicfu_version"
        }

        posixTest
        posixMain { dependsOn commonMain }

        if (!project.ext.ideaActive) {
            configure([iosArm32Main, iosArm64Main, iosX64Main, macosX64Main, linuxX64Main, mingwX64Main]) {
                dependsOn posixMain
            }

            configure([iosArm32Test, iosArm64Test, iosX64Test, macosX64Test, linuxX64Test, mingwX64Test]) {
                dependsOn posixTest
            }
        }
    }
}

project.ext.set("hasNative", true)
