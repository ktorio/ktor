/*
 * Copyright 2014-2025 JetBrains s.r.o and contributors. Use of this source code is governed by the Apache 2.0 license.
 */

apply plugin: 'binary-compatibility-validator'

apiValidation {
    def excludeList = [
        'ktor',
        "ktor-client-test-base",
        'ktor-client-tests',
        'ktor-client-js',
        'ktor-client-content-negotiation-tests',
        'ktor-serialization-kotlinx-tests',
        'ktor-serialization-tests',
        'ktor-client',
        'ktor-client-plugins',
        'ktor-server-test-suites',
        "ktor-server-test-base",
        'ktor-test-base',
    ].toSet()

    def projects = [].toSet()
    def queue = [
        project(":ktor-client"),
        project(":ktor-http"),
        project(":ktor-network"),
        project(":ktor-utils"),
        project(":ktor-io"),
        project(":ktor-server"),
        project(":ktor-server:ktor-server-plugins"),
        project(":ktor-shared"),
    ]

    while (!queue.isEmpty()) {
        def project = queue.remove(queue.size() - 1) as Project
        if (projects.add(project)) {
            queue += project.childProjects.values()
        }
    }

    def projectNames = projects.collect { it.name }.toSet()

    ignoredProjects += excludeList
    ignoredProjects += project.allprojects.collect { it.name }.findAll { !projectNames.contains(it) }

    klib {
        enabled = true
    }
}
