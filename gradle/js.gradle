apply plugin: 'com.moowork.node'

node {
    version = "$nodejs_version"
    npmVersion = "$npm_version"
    download = false
    nodeModulesDir = file(buildDir)
}

kotlin {
    targets {
        fromPreset(presets.js, 'js')
    }

    sourceSets {
        jsMain.dependencies {
            languageSettings {

            }
            api "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"

            api group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core-js', version: coroutines_version
            api group: 'org.jetbrains.kotlinx', name: 'kotlinx-io-js', version: kotlinx_io_version
            api group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-io-js', version: kotlinx_io_version
            api group: 'org.jetbrains.kotlinx', name: 'atomicfu-js', version: atomic_fu_version
        }
        jsTest.dependencies {
            api "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
        }
    }

}

compileKotlinJs {
    kotlinOptions.metaInfo = true
    kotlinOptions.sourceMap = true
    kotlinOptions.moduleKind = 'umd'
    kotlinOptions.main = 'noCall'
    kotlinOptions.sourceMapEmbedSources = 'always'
}

compileTestKotlinJs {
    kotlinOptions.metaInfo = true
    kotlinOptions.sourceMap = true
    kotlinOptions.moduleKind = 'umd'
    kotlinOptions.main = 'call'
    kotlinOptions.sourceMapEmbedSources = 'always'
}


task installDependenciesMochaChrome(type: NpmTask, dependsOn: [npmInstall]) {
    args = [
        'install',
        "mocha@$mocha_version",
        "mocha-headless-chrome@$mocha_headless_chrome_version",
        '--no-save'
    ]

    if (project.hasProperty("teamcity")) {
        args += ["mocha-teamcity-reporter@$mocha_teamcity_reporter_version"]
    }
}

def mochaChromeTestPage = file("$buildDir/test-page.html")

task prepareMochaChrome(dependsOn: [compileTestKotlinJs, installDependenciesMochaChrome]) {
    outputs.file(mochaChromeTestPage)
}

prepareMochaChrome.doLast {
    def reporter = (project.hasProperty("teamcity")) ? "mocha.setup({reporter: 'teamcity'});" : ""
    mochaChromeTestPage.text = """<!DOCTYPE html>
        <html>
        <head>
            <title>Mocha Tests</title>
            <meta charset="utf-8">
            <link rel="stylesheet" href="$node.nodeModulesDir/node_modules/mocha/mocha.css">
        </head>
        <body>
        <div id="mocha"></div>
        <script src="$node.nodeModulesDir/node_modules/mocha/mocha.js"></script>
        <script src="$node.nodeModulesDir/node_modules/mocha-teamcity-reporter/lib/teamcityBrowser.js"></script>
        <script>mocha.timeout(10000000);</script>
        <script>mocha.setup('bdd');</script>
        <script>
            $reporter
            mocha.checkLeaks();
        </script>
        <script src="$projectDir/build/web/kotlin.js"></script>
        <script src="$projectDir/build/web/kotlin-test.js"></script>
        <script src="$projectDir/build/web/kotlinx-atomicfu.js"></script>
        <script src="$projectDir/build/web/kotlinx-coroutines-core.js"></script>
        <script src="$projectDir/build/web/kotlinx-io-js.js"></script>
        <script src="$projectDir/build/web/kotlinx-coroutines-io-js.js"></script>
        <script src="$projectDir/build/web/kotlinx-serialization-runtime-js.js"></script>
        <script src="$projectDir/build/web/ktor-utils-js.js"></script>
        <script src="$projectDir/build/web/ktor-http-js.js"></script>
        <script src="$projectDir/build/web/ktor-client-core-js.js"></script>
        <script src="$projectDir/build/web/ktor-client-js.js"></script>
        <script src="$projectDir/build/web/ktor-client-json-js.js"></script>
        <script src="$compileTestKotlinJs.outputFile"></script>
        <script>mocha.run();</script>
        </body>
        </html>
    """
}

task testMochaChrome(type: NodeTask, dependsOn: prepareMochaChrome) {
    script = file("$node.nodeModulesDir/node_modules/mocha-headless-chrome/bin/start")
    args = [
        compileTestKotlinJs.outputFile, '--file', mochaChromeTestPage,
        '-a', 'no-sandbox', '-a', 'disable-setuid-sandbox',
        '--reporter', 'mocha-teamcity-reporter'
    ]

    if (project.hasProperty('mochaTests')) args += ['--grep', "$mochaTests"]
}

task assembleWeb(type: Sync) {
    from(files {
        configurations.jsTestRuntimeClasspath.collect { File file ->
            file.name.endsWith(".jar") ?
                zipTree(file.absolutePath).matching {
                    include '*.js'
                    include '*.js.map'
                } :
                files()
        }
    }.builtBy(configurations.jsTestRuntimeClasspath))
    from compileKotlinJs.destinationDir
    into "${projectDir}/build/web"
    dependsOn jsMainClasses
}

assemble.dependsOn assembleWeb

if (project.hasProperty("enable-js-tests")) {
    jsTest.dependsOn testMochaChrome
}
