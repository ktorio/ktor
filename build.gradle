buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'net.researchgate:gradle-release:2.4.0'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

apply plugin: 'net.researchgate.release'

def configuredVersion = project.hasProperty('releaseVersion') ? project.releaseVersion : project.version

allprojects {
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'kotlin'
    apply plugin: 'org.jetbrains.dokka'

    apply plugin: 'com.jfrog.bintray'

    group = 'io.ktor'
    version = configuredVersion

    configurations {
        testOutput.extendsFrom(testCompile)
        boot
    }

    sourceSets {
        main {
            java {
                srcDir "src"
            }
            kotlin {
                srcDir "src"
            }
            resources {
                srcDir "resources"
            }
        }
        test {
            kotlin {
                srcDir "test"
            }
            resources {
                srcDir "test-resources"
            }
        }
    }

    kotlin {
        experimental {
            coroutines 'enable'
        }
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/kdoc"
    }

    task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
        outputFormat = 'javadoc'
        outputDirectory = "$buildDir/javadoc"
    }

    task jarTest(type: Jar, dependsOn: testClasses) {
        from sourceSets.test.output
        classifier = 'test'
    }

    test {
        ignoreFailures = true
        maxHeapSize = "2g"
        exclude("**/*StressTest*")
    }

    task stressTest(type: Test, dependsOn: testClasses) {
        ignoreFailures = true
        maxHeapSize = "2g"
        include("**/*StressTest*")
    }

    dependencies {
        testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit', version: kotlin_version
        testCompile group: 'junit', name: 'junit', version: junit_version
    }

    repositories {
        mavenLocal()

        maven { url "https://jcenter.bintray.com" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap-1.1" }
        maven { url "https://jitpack.io" }
        maven { url "http://repo.maven.apache.org/maven2" }
        maven { url "https://dl.bintray.com/kotlinx/kotlinx" }
        maven { url "https://dl.bintray.com/kotlin/kotlinx" }
        maven { url "https://dl.bintray.com/kotlin/exposed" }
    }

    task packageSources(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: 'dokkaJavadoc') {
        classifier = 'javadoc'
        from fileTree(new File(project.buildDir, 'javadoc'))
    }

    task dokkaJar(type: Jar, dependsOn: 'dokka') {
        classifier = 'kdoc'
        from fileTree(new File(project.buildDir, 'kdoc'))
    }

    artifacts.archives packageSources
    artifacts.archives javadocJar
    artifacts.archives dokkaJar

    publishing {
        publications {
            mavenProject(MavenPublication) {
                from components.java
                groupId project.group
                artifactId project.name
                version project.version

                artifact packageSources {
                    classifier 'sources'
                }

                artifact javadocJar {
                    classifier 'javadoc'
                }

                artifact dokkaJar {
                    classifier 'kdoc'
                }
            }
        }
    }

    if (!project.path.contains("ktor-samples")) {
        bintray {
            user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
            key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
            publish = true

            pkg {
                userOrg = 'kotlin'
                repo = 'ktor'
                name = 'ktor'

                publications = ['mavenProject']

                version {
                    name = project.version
                    released = new Date()
                    vcsTag = project.version
                }
            }
        }
    }
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    dependencies {
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre7', version: kotlin_version
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8', version: kotlin_version
        compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-jdk8', version: coroutines_version
        compile(group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-io', version: coroutines_version) {
            exclude(module: 'kotlin-stdlib')
        }
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        compile group: 'com.typesafe', name: 'config', version: '1.3.1'
        testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit', version: kotlin_version
        testCompile group: 'junit', name: 'junit', version: junit_version
    }

}

release {
    scmAdapters = [net.researchgate.release.GitAdapter]
    git {
        requireBranch = 'gradle-build'
    }
}

afterReleaseBuild.dependsOn bintrayUpload
