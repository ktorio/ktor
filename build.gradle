buildscript {
    ext.kotlin_repository = {
        if (kotlin_version.endsWith('-SNAPSHOT')) return 'https://oss.sonatype.org/content/repositories/snapshots'
        return 'http://dl.bintray.com/kotlin/kotlin-eap' }()

    /*
     * When true, kotlinx.coroutines version is updated to $version-SNAPSHOT.
     * Set by teamcity agent
     */
    ext.useCoroutinesSnapshot = rootProject.properties['coroutinesSnapshot'] != null

    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url kotlin_repository }
        maven { url 'https://dl.bintray.com/jetbrains/kotlin-native-dependencies' }
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_plugin_version"
        classpath 'net.researchgate:gradle-release:2.4.0'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:$kotlin_native_version"
        classpath "org.jetbrains.kotlinx:atomicfu-gradle-plugin:$atomic_fu_version"
        classpath "com.moowork.gradle:gradle-node-plugin:$gradle_node_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

apply plugin: 'net.researchgate.release'

ext.configuredVersion = project.hasProperty('releaseVersion') ? project.releaseVersion : project.version
ext.dokkaExcludes = [
    'ktor-server',
    'ktor-samples',
    'ktor-features',
    'ktor-client'
]

ext.globalM2 = "$buildDir/m2"
ext.publishLocal = project.hasProperty('publishLocal')

apply from: 'gradle/platform.gradle'
apply from: 'gradle/experimental.gradle'

allprojects {
    def platform = platformOf(project)

    group = 'io.ktor'
    version = configuredVersion
    if (useCoroutinesSnapshot) {
        repositories {
            maven { url "https://oss.jfrog.org/oss-snapshot-local/org/jetbrains/kotlinx/" }
        }

        configurations.all {
            resolutionStrategy {
                eachDependency { DependencyResolveDetails details ->
                    if (details.requested.group == 'org.jetbrains.kotlinx' && details.requested.name.contains("kotlinx-coroutines")
                        && !details.requested.name.contains("kotlinx-coroutines-io")) {
                        details.useVersion "$coroutines_version-SNAPSHOT"
                    }
                }
            }
        }
    }

    repositories {
        mavenLocal()
        maven { url 'https://dl.bintray.com/kotlin/kotlinx/' }
        maven { url kotlin_repository }
        maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
        jcenter()
    }

    apply plugin: "kotlin-platform-${platform}"
    apply from: rootProject.file("gradle/atomicfu-${platform}.gradle")
    apply from: rootProject.file("gradle/${platform}.gradle")

    apply from: rootProject.file('gradle/publish.gradle')

    if (project.parent != null && project.parent.name == 'ktor-server') {
        apply from: rootProject.file('gradle/engines.gradle')
    }

    task sourceJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        if (platform != 'native') {
            from sourceSets.main.kotlin
            duplicatesStrategy DuplicatesStrategy.EXCLUDE
            def platformSrc = sourceSets.main.allSource
            def commonSrc = rootProject.sourceSets.main.allSource
            from(platformSrc + commonSrc)
        }
    }

    task emptyJar(type: Jar) {
        classifier 'javadoc'
    }

    tasks.build.dependsOn(['sourceJar', 'emptyJar'])

}

release {
    scmAdapters = [net.researchgate.release.GitAdapter]
    git {
        requireBranch = 'gradle-build'
    }
}

afterReleaseBuild.dependsOn bintrayUpload
